# Saved results: neurons-metadata-2.json
# Neurons with metadata Version 2 for SCKAN NLI. This one includes literature citations and IRI for the species.

PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX partOf: <http://purl.obolibrary.org/obo/BFO_0000050>
PREFIX ilxtr: <http://uri.interlex.org/tgbugs/uris/readable/>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>

SELECT DISTINCT ?Neuron_IRI ?Neuron_Label ?Neuron_Pref_Label ?Species_IRI
                (GROUP_CONCAT(DISTINCT ?Species_1; SEPARATOR=", ") AS ?Species)
                ?Sex
                (GROUP_CONCAT(DISTINCT ?Phenotype; SEPARATOR=", ") AS ?Phenotypes)
                (GROUP_CONCAT(DISTINCT ?Forward_Connection; SEPARATOR=", ") AS ?Forward_Connections)
                ?Alert
                ?Reference
                (GROUP_CONCAT(DISTINCT ?Literature_Citation; SEPARATOR=", ") AS ?Citations)

WHERE
{
    ?Neuron_IRI rdfs:subClassOf+ ?sckan_neuron . #only clonsidering SCKAN neurons 
    values ?sckan_neuron { ilxtr:NeuronSparcNlp ilxtr:NeuronApinatSimple }


    OPTIONAL {?Neuron_IRI rdfs:label ?Neuron_Label.}
    OPTIONAL {?Neuron_IRI skos:prefLabel ?Neuron_Pref_Label.}
            
    OPTIONAL {
                ?Neuron_IRI ilxtr:isObservedInSpecies ?Species_IRI.
                ?Species_IRI rdfs:label ?Species_1.
             }
    OPTIONAL {?Neuron_IRI ilxtr:hasPhenotypicSex/rdfs:label ?Sex.}
    OPTIONAL {?Neuron_IRI (  ilxtr:literatureCitation 
                            |rdfs:subClassOf/ilxtr:literatureCitation) ?Literature_Citation.}
    OPTIONAL {?Neuron_IRI ilxtr:reference ?Reference.}

    OPTIONAL {?Neuron_IRI ilxtr:alertNote ?Alert.}
    OPTIONAL {?Neuron_IRI ilxtr:hasForwardConnection ?Forward_Connection.}
    OPTIONAL {?Neuron_IRI (ilxtr:hasNeuronalPhenotype | 
                            ilxtr:hasFunctionalCircuitRole |
                            ilxtr:hasCircuitRole |
                            ilxtr:hasProjection  
                           )/rdfs:label ?Phenotype.}
}
GROUP BY ?Neuron_IRI ?Neuron_Label ?Neuron_Pref_Label ?Species_IRI ?Sex ?Alert ?Reference
ORDER BY ?Neuron_IRI
